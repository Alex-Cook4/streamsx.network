/*
** Copyright (C) 2016  International Business Machines Corporation
** All Rights Reserved
*/

namespace sample;

use com.ibm.streamsx.network.ipv4::*;
use com.ibm.streamsx.network.ipv6::*;
use com.ibm.streamsx.network.dns::*;

composite LiveDNSPacketDPDKSourceFilter {

    param
    expression<float64> $metricsInterval: (float64)getSubmissionTimeValue("metricsInterval", "1.0" );
    expression<float64> $timeoutInterval: (float64)getSubmissionTimeValue("timeoutInterval", "10.0" );

    type

    DNSMessageType =
        uint64 packetNumber,            // sequence number of packet
        float64 captureTime,             // time that packet was captured, in seconds since Unix epoch
        uint32 ipv4ServerAddress,       // IPv4 address of DNS server
        boolean isResponse,
        uint8 opCode,
        uint8 responseCode,
        rstring questionName,
        uint16 questionType,
        list<rstring> answerNames,
        list<uint16> answerTypes,
        list<rstring> nameserverNames,
        list<rstring> additionalNames,
        list<rstring> additionalData;

    FlattenedDNSMessageType = 
        float64 captureTime,
        rstring flattenedDNSMessage;

    DNSMalformedType =
      uint64 packetNumber,
      float64 captureTime, 
      rstring error,
      blob dnsMessage;

    MetricsType =
        float64 metricsTime,            // time that metrics were produced, in seconds since Unix epoch
        boolean metricsUpdated,         // true if metrics have been updated since previous output tuple
        uint64 packetNumber,            // sequence number of packet, as emitted by operator
        uint64 packetsReceived,         // number of packets received by network interface in this interval
        uint64 packetsDropped,          // number of packets dropped in this interval
        uint64 packetsProcessed,        // number of packets emitted by operator in this interval
        uint64 bytesProcessed,         // number of bytes emitted by operator in this interval
        float64 intervalElapsed,        // elapsed time since last metrics interval, in seconds
        uint64 intervalPacketsReceived, // number of packets received since last metrics interval
        uint64 intervalPacketsDropped,  // number of packets dropped since last metrics interval
        uint64 intervalPacketsProcessed,// number of packets processed since last metrics interval
        uint64 intervalBytesProcessed; // number of bytes processed since last metrics interval

    graph



 ( stream<DNSMessageType> DNSMessageStream ; 
   stream<FlattenedDNSMessageType> FlattenedDNSQuestionStream ;
   stream<FlattenedDNSMessageType> FlattenedDNSNormalResponseStream ;
   stream<FlattenedDNSMessageType> FlattenedDNSErrorResponseStream ;
   stream<DNSMalformedType> DNSMalformedStream ;
   stream<MetricsType> MetricsStream ) = DNSPacketDPDKSource() {
        param
            lcoreMaster: 0; // run the master thread on core 0.
            lcore: 1; // run this ingest thread on core 1.
            nicPort: 0; // bind to NIC port 0
            nicQueue: 0; // bind to NIC queue 0
            promiscuous: true;
            outputFilters: !parseError() && DNS_RESPONSE_FLAG(), // selects messages for DNSMessageStream
                           !parseError() && !DNS_RESPONSE_FLAG(), // selects messages for FlattenedDNSQuestionStream
                           !parseError() && DNS_RESPONSE_FLAG() && DNS_RESPONSE_CODE()==0ub, // selects messages for FlattenedDNSNormalResponseStream
                           !parseError() && DNS_RESPONSE_FLAG() && DNS_RESPONSE_CODE()!=0ub, // selects messages for FlattenedDNSErrorResponseStream
                           parseError(), // selects messages for DNSMalformedStream
                           metricsUpdated(); // selects messages for MetricsStream
            metricsInterval: $metricsInterval;
        output 
        DNSMessageStream:
          packetNumber = packetsProcessed(),
          captureTime = (float64)CAPTURE_SECONDS() + (float64)CAPTURE_MICROSECONDS() / 1000000.0,
          ipv4ServerAddress = DNS_RESPONSE_FLAG() ? IPV4_SRC_ADDRESS() : IPV4_DST_ADDRESS(), 
          isResponse = DNS_RESPONSE_FLAG(),
          opCode = DNS_OPCODE(),
          responseCode = DNS_RESPONSE_CODE(),
          questionName = DNS_QUESTION_NAME(),
          questionType = DNS_QUESTION_TYPE(),
          answerNames = DNS_ANSWER_NAMES(),
          answerTypes = DNS_ANSWER_TYPES(),
          nameserverNames = DNS_NAMESERVER_NAMES(),
          additionalNames = DNS_ADDITIONAL_NAMES(),
          additionalData = DNS_ADDITIONAL_DATA();
        FlattenedDNSQuestionStream:
          captureTime = (float64)CAPTURE_SECONDS() + (float64)CAPTURE_MICROSECONDS() / 1000000.0,
          flattenedDNSMessage = DNS_ALL_FIELDS("\n", "|", "^"); 
        FlattenedDNSNormalResponseStream:
          captureTime = (float64)CAPTURE_SECONDS() + (float64)CAPTURE_MICROSECONDS() / 1000000.0,
          flattenedDNSMessage = DNS_ALL_FIELDS("\n", "|", "^"); 
        FlattenedDNSErrorResponseStream:
          captureTime = (float64)CAPTURE_SECONDS() + (float64)CAPTURE_MICROSECONDS() / 1000000.0,
          flattenedDNSMessage = DNS_ALL_FIELDS("\n", "|", "^"); 
        DNSMalformedStream:
          packetNumber = packetsProcessed(),
          captureTime = (float64)CAPTURE_SECONDS() + (float64)CAPTURE_MICROSECONDS() / 1000000.0,
          error = parseErrorDescription(),
          dnsMessage = PAYLOAD_DATA();
        MetricsStream:
            metricsTime = getTimestampInSecs(),
            metricsUpdated = metricsUpdated(),
            packetNumber = packetsProcessed() - 1ul,
            packetsReceived = packetsReceived(),
            packetsDropped = packetsDropped(),
            packetsProcessed = packetsProcessed(),
            bytesProcessed = bytesProcessed(),
            intervalElapsed = metricsIntervalElapsed(),
            intervalPacketsReceived = metricsIntervalPacketsReceived(),
            intervalPacketsDropped = metricsIntervalPacketsDropped(),
            intervalPacketsProcessed = metricsIntervalPacketsProcessed(),
            intervalBytesProcessed = metricsIntervalBytesProcessed();
    }
    () as DNSMessageSink = FileSink(DNSMessageStream) { param file: "debug.LiveDNSPacketDPDKSourceFilter.DNSMessageStream.out"; format: txt; hasDelayField: true; }
    () as FlattenedDNSQuesstionSink = FileSink(FlattenedDNSQuestionStream) { param file: "debug.LiveDNSPacketDPDKSourceFilter.FlattenedDNSQuestionStream.out"; format: line; suppress: captureTime; }
    () as FlattenedDNSNormalResponseSink = FileSink(FlattenedDNSNormalResponseStream) { param file: "debug.LiveDNSPacketDPDKSourceFilter.FlattenedDNSNormalResponseStream.out"; format: line; suppress: captureTime; }
    () as FlattenedDNSErrorResponseSink = FileSink(FlattenedDNSErrorResponseStream) { param file: "debug.LiveDNSPacketDPDKSourceFilter.FlattenedDNSErrorResponseStream.out"; format: line; suppress: captureTime; }
    () as DNSMalformedSink = FileSink(DNSMalformedStream) { param file: "debug.LiveDNSPacketDPDKSourceFilter.DNSMalformedStream.out"; format: txt; hasDelayField: true; }
    () as MetricsSink = FileSink(MetricsStream) { param file: "debug.LiveDNSPacketDPDKSourceFilter.MetricsStream.out"; format: txt; hasDelayField: true; }

    () as Timeout = Custom() { logic onProcess: { block($timeoutInterval); shutdownPE(); } }
}
